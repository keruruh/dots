#! /usr/bin/env nu

def write-file [
    file: string
    label: string
    packages: list<string>
] {
    let current_date = date now | format date "%Y-%m-%d %H:%M:%S"
    let count = $packages | length

    let header = [
        $"# Generated by: list-packages.nu --($label)"
        $"# Generated at: ($current_date)"
        $"# Total ($label) packages: ($count)"
        "\n"
    ]

    $header | str join "\n" | save --force $file
    $packages | str join "\n" | save --append --force $file
}

def main [
    --native (-n)
    --foreign (-f)
    --all (-a)
] {
    let mode = (
        if $all { "all" }
        else if $native { "native" }
        else if $foreign { "foreign" }
        else { "all" }
    )

    let native_pkgs = pacman --query --quiet --explicit --native | lines
    let foreign_pkgs = pacman --query --quiet --explicit --foreign | lines

    let file_native  = $nu.home-path | path join "packages-native.txt"
    let file_foreign = $nu.home-path | path join "packages-foreign.txt"

    if $mode == "native" or $mode == "all" {
        write-file $file_native "native" $native_pkgs
    }

    if $mode == "foreign" or $mode == "all" {
        write-file $file_foreign "foreign" $foreign_pkgs
    }
}
